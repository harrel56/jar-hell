import org.apache.tools.ant.taskdefs.condition.Os

def npmExec = Os.isFamily(Os.FAMILY_WINDOWS) ? 'npm.cmd' : 'npm'

configurations {
    output
}

tasks.register('clean') {
    delete files('dist')
}

tasks.register('npmCi', Exec) {
    commandLine npmExec, 'ci'
    standardOutput System.out
    inputs.file('package-lock.json')
    outputs.file('package.json') // just a stub to define any output
}

tasks.register('build', Exec) {
    dependsOn(npmCi)
    commandLine npmExec, 'run', 'build'
    standardOutput System.out
    inputs.files(fileTree('.').matching {
        exclude 'dist', 'node_modules'
    }.findAll())
    outputs.dir('dist')
}

artifacts {
    output(tasks.build.outputs.files.singleFile) {
        builtBy('build')
    }
}